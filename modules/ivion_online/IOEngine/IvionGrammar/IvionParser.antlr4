parser grammar IvionParser;
options { tokenVocab = IvionLexer; }

//////////////////////////////////////////////////////////////////////////// PLAYER


playerType
    : Player
    | Enemy
    | Ally
    ;
playerPreFix
    : Another
    | Controlled
    | Uncontrolled
    ;
playerPostFix
    : In Terrain 
    | Near Terrain 
    | Near CARD_NAME 
    | In CARD_NAME 
    | In tile
    | At Least literalValue Tile Away
    ;

playerFilter
    : playerPreFix* playerType playerPostFix*
    ;

currentPlayer
    : Yourself
    | You
    | Your
    ;
previousPlayer
    : Them
    | They
    | That playerType
    | Their
    ;
playerRef
    : previousPlayer
    | currentPlayer
    ;
targetPlayer
    : Another? Target playerFilter
    ;
previousCardsController
    : previousCard Controller
    | Its Controller
    ;
singlePlayer
    : targetPlayer
    | playerRef
    | previousCardsController
    ;
multiPlayer
    : UpToFlag=upToFlag? literalValue targetPlayer
    ;
allOtherPlayers
    : (All | Each) Other playerFilter
    ;
allPlayers
    : (Each | All | Any) playerFilter
    | Yourself Or Another Player
    ;

////
player
    : singlePlayer
    | multiPlayer
    | allPlayers
    | allOtherPlayers
    ;

hypotheticalPlayer
    : A? playerFilter
    | player
    ;

//////////////////////////////////////////////////////////////////////////// CARD

cardPrefix
    : Enemy
    | Attached
    | Non? Ability
    | Non? Attack
    | Revealed
    | Another
    | First
    | Second
    | Third
    ;
cardPostfix
    : In Terrain
    | Near Terrain
    | Near hypotheticalPlayer
    | Near hypotheticalTile
    | In hypotheticalTile
    | Targeting hypotheticalPlayer
    | With A Resource Cost Of literalValue Or Less
    | Attached To hypotheticalPlayer
    | Attached To hypotheticalTile
    | Controlled By hypotheticalPlayer
    | With Hardcast
    | With Frenzy
    ;
cardFilter
    : cardPrefix* Card cardPostfix*
    ;
cardName
    : CARD_NAME
    ;
bottomCard
    : The Bottom Card Of hypotheticalPlayer Deck
    ;
previousCard
    : That Card
    | The Card
    ;
targetCard
    : Target cardFilter
    ;
singleCard
    : cardName
    | targetCard
    | bottomCard
    | previousCard
    ;
allCards
    : All cardFilter
    ;
multiCard
    : UpToFlag=upToFlag? literalValue targetCard
    ;
////
card
    : singleCard
    | allCards
    | multiCard
    ;

hypotheticalCard
    : A? cardFilter
    | card
    ;

//////////////////////////////////////////////////////////////////////////// TILE

tilePreFix
    : Non? Empty
    ;
tilePostFix
    : With Terrain 
    | Near Terrain 
    | Near hypotheticalPlayer
    | Near card
    | At Least literalValue Tile Away
    | At Least literalValue Tile Away From hypotheticalPlayer
    | That Already Had Terrain
    ;

tileFilter
    : tilePreFix* Tile tilePostFix*
    ;

targetTile: Another? literalValue? Target tileFilter;

previousTile
    : That Tile
    | The Tile
    ;
theNearestTile
    : The Nearest tileFilter Next To (hypotheticalPlayer | hypotheticalCard)
    ;
playersTile
    : The Tile player (Are | Is) In
    ;
cardsTile
    : This Tile
    ;
singleTile
    : targetTile
    | previousTile
    | theNearestTile
    | playersTile
    | cardsTile
    ;

multiTile
    : UpToFlag=upToFlag? literalValue targetTile
    ;

allTiles
    : (Each | All) tileFilter
    ;

////

tile
    : singleTile
    | allTiles
    | multiTile
    ;
hypotheticalTile
    : A? tileFilter
    | tile
    ;

entity
    : card
    | player
    | tile
    ;

hypotheticalEntity
    : hypotheticalCard
    | hypotheticalPlayer
    | hypotheticalTile
    ;


//////////////////////////////////////////////////////////////////////////// EFFECTS


disarmPlayer
	: Disarm literalValue player
	| Disarm literalValue
	;

silencePlayer
	: Silence literalValue player
	| Silence literalValue
	;

slowPlayer
	: Slow literalValue player
	| Slow literalValue
	;

controlPlayer
	: Apply literalValue More Instances Of A Control Type Of Your Choice To player
	| Apply A Instance Of A Control Type Of Your Choice To player
	| Increase The Value Of A Control Type Already Affecting player By literalValue
	;

awayFlag
    : Away From player
    ;
upToFlag
    : Up To
    ;
travelDistance
    : Travel TargetPlayer=player? UpToFlag=upToFlag? Another? literalValue Tile Awayflag=awayFlag?
    ;

travelPlayer
	: Travel TargetPlayer=player? To (TargetTile=tile | TargetCard=card)
	;

destroyCard
	: Destroy card
	;

dealDamage
	: Deal literalValue Damage To player
	| Deal Damage To player Equal To gameStateValue
	| Deal literalValue Damage To player Plus Damage Equal To gameStateValue
	| Deal literalValue Damage To
	| Deal Damage To player
	| Deal literalValue More Damage To player
	| Deal literalValue Damage To player Plus literalValue More Damage For Each Revealed Card In Their Hand
	| Deal Damage To player Equal To literalValue
	;

counterCard
	: Counter card
	;

attachCard
	: Attach To entity
	| Attach To entity Until turnTrigger
	| Attach CARD_NAME To entity
	;

gainPower
	: player Gain literalValue POWER
	| Gain literalValue More POWER
	| Gain literalValue Additional POWER
	| Gain literalValue POWER
	| Gain literalValue More POWER
	;

gainAction
	: player Gain literalValue ACTION
	| Gain literalValue More ACTION
	| Gain literalValue Additional ACTION
	| Gain literalValue ACTION
	| Gain literalValue More ACTION
	;

gainInitiative
	: Gain INITIATIVE+
	| player Gain INITIATIVE+
	| Gain Another INITIATIVE+
	;

gainMitigate
	: player Gains Mitigate literalValue
	| Gain Mitigate literalValue
	;

loseMitigate
	: Lose Your Mitigate
	;

useSecondWind
	: Use Your Second Wind
	;

returnCard
	: Return Target Attached Card To Its Owners Hand
	| Return Target Card With A Resource Cost Of literalValue Or Less From Your Discard To Your Hand
	| Return Target Ability Card From Your Discard To Your Hand
	| Return A Attack Card With A Resource Cost Of literalValue Or Less From Your Discard To Your Hand
	| Return A Card From Your Discard To Your Hand
	| Return Target Card From Your Discard To Your Hand
	| Return CARD_NAME To Your Hand
	| Return It From Your Discard To Your Hand
	| Return Target Card With A Resource Cost Of literalValue Or More From Your Discard To Your Hand
	;

putBottomCardOfDeckIntoHand
	: Put hypotheticalCard Into Your Hand
	;

triggerDurationEffects
	: Trigger Your Duration Effects As Though Your Turn Had Just Ended
	;

playCard
	: Play A Card
	| Play A Card For literalValue Less Resource
	| Play A Card For Free
	| Play That Card For Free
	| Play Target Non Attack Card From Your Discard Targeting hypotheticalPlayer For Free
	| Play A Attack Card Targeting hypotheticalPlayer
	| Play A Ability Card From Your Discard Targeting player
	| Play A Attack Card Targeting player For literalValue Less Resource
	| Play A Card With Frenzy For literalValue Less Resource
	| Play A Card Targeting player
	| Play Cursed Dagger For Free P This Doesnt Count Towards Your Cursed Dagger Limit Per Turn
	| Play Runic Slaughter Targeting player For Free P This Doesnt Count Towards Your Runic Slaughter Limit Per Turn
	| Play A Card For literalValue Less Resource
	| Play A Non Attack Card Targeting hypotheticalPlayer
	| Play A Ability Card Targeting player For literalValue Less Resource
	;

discardCards
	: Discard literalValue Card
	| Discard A Card
	| Discard A Revealed Card Of player Choice
	| player Discard literalValue Card
	| player Discard A Card
	| player Discards Target Revealed Card
	| player Discards literalValue Card
	| player Discards A Revealed Card Of player Choice
	| Discard A Revealed Card
	;

gainsHeroic
	: That Card Gains Heroic
	;

healPlayer
	: Heal player literalValue HP
	| Heal player literalValue HP
	| Heal player literalValue HP
	| Heal player HP Equal To The Number Of hypotheticalCard In Their Hand
	;

stunPlayer
	: Stun player
	;

revealCards
	: player Reveals literalValue Card From Their Hand
	| player Reveal literalValue Card From Their Hand
	| player Reveal A Card From Their Hand
	| player Reveals A Card From Their Hand
	| player Reveals literalValue Card From Their Hand
	| player Reveals A Card From Their Hand
	| Reveal CARD_NAME
	| Reveal A Card From Your Hand
    | Reveals A Card From Their Hand
	;



drawCards
	: Draw A Card
	| Draw literalValue Card
	| Draw A Additional Card
	| player Draw literalValue Card
	| player Draw literalValue Card
	| player Draw A Card
	;

removeControl
	: Remove Up To literalValue Instances Of Control From player
	| Remove All Control Affecting player
    | Remove A Instance Of Control From player
	;

hardcastEffect
	: Hardcast
	;

seekEffect
	: Seek
	;

makeTerrain
	: Make tile Difficult Terrain
	;

removeTerrain
	: Remove Target Terrain Token
	;

spendResources
	: Spend literalValue Resource
	;

scryEffect
	: Look At The Top literalValue Card Of Your Deck P Put Any Number On The Bottom And The Rest On Top In Any Order
	;

overrideFrenzy
	: That Card Has Frenzy Even If No Player Has Used Their Second Wind
	;

increaseCardDuration
	: Increase The Duration Of A Card You Control By literalValue
	;

resetUseOfCard
	: Reset The Use Of CARD_NAME
	;

heroic: Heroic;

//////////////////////////////////////////////////////////////////////////// PASSIVE EFFECTS


onlyPlayCertainCards
	: hypotheticalPlayer May Only Play Card Named CARD_NAME
	| Only Play CARD_NAME If hypotheticalPlayer (Have | Has) Resolve A Attack Card This Turn
	| Only Play CARD_NAME If hypotheticalPlayer (Is | Are) Controlled
	| Only Play CARD_NAME If hypotheticalPlayer (Is | Are) Uncontrolled
	| Only Play CARD_NAME If hypotheticalPlayer (Is | Are) Near Terrain
	| Only Play CARD_NAME If hypotheticalPlayer (Have | Has) At Least literalValue Card In Your Discard
	| Only Play CARD_NAME If hypotheticalPlayer (Have | Has) Taken literalValue Or More Damage This Turn
	;

costReduction
	: hypotheticalCard hypotheticalPlayer Play Cost literalValue Less Resource
    | The Second Attack Card hypotheticalPlayer Play During Your Turn Cost literalValue Less Resource
	;

rangeSet
	: The Range Of Your Attack Card Is literalValue
	;

cantPlayCards
	: hypotheticalPlayer Cant Play Card
	;

playerCantBeTargeted
	: hypotheticalPlayer Cant Be Targeted By Enemies
	| hypotheticalPlayer Cant Be Targeted By Card
	;

increasedHandSize
    : Your Maximum Hand Size Is Increased By literalValue
    ;

increasedMaxHP
    : Your Maximum HP Is Increased By literalValue
    ;

replacesSecondWind
    : CARD_NAME Replaces Your Second Wind
    ;

noInitiative
    : hypotheticalPlayer Dont Gain INITIATIVE At The Start Of Your Turn
    ;
    
drawRange
	: All Of hypotheticalPlayer Card C Except Travel Card That Target A Tile C Also Draw Range From CARD_NAME
	| All Of hypotheticalPlayer Card C Except Travel Card That Target A Tile C Also Draw Range From hypotheticalTile
	;

gainRange
	: All Of hypotheticalPlayer Card C Except Travel Card That Target A Tile C Gain literalValue Range
    ;

limitTargets
    : Only Target Other hypotheticalPlayer condition
    ;

passiveEffect
    : playerCantBeTargeted
    | cantPlayCards
    | rangeSet
    | costReduction
    | onlyPlayCertainCards
    | increasedHandSize
    | increasedMaxHP
    | replacesSecondWind
    | noInitiative
    | drawRange
    | gainRange
    | limitTargets
    ;

//////////////////////////////////////////////////////////////////////////// PASSIVE TRIGGERS


durationEffect
	: Duration 
	;

startOfPlayerTurn
	: The Start Of hypotheticalPlayer Turn
	| The Start Of hypotheticalPlayer Turn
    ;

endOfPlayerTurnTrigger
    : The End Of hypotheticalPlayer Turn
	;
    
startOfPlayerNextTurn
    : The Start Of hypotheticalPlayer Next Turn
    ;

endOfTurnTrigger
    : The End Of The Turn
	| The End Of Turn
    | End Of Turn
	;

turnTrigger
    : startOfPlayerTurn
    | endOfPlayerTurnTrigger
    | startOfPlayerNextTurn
    | endOfTurnTrigger
    ;

gameStartTrigger
	: At The Start Of The Game
	;

effectPostfix
    : For The First Time During playerRef Turn
    | For The First Time During Each Turn 
    | During Each Turn
    | During playerRef Turn
    ;

afterEffectTrigger
	: hypotheticalPlayer Resolve hypotheticalCard
	| hypotheticalCard Resolves
	| hypotheticalPlayer Resolve playerRef First hypotheticalCard
	| hypotheticalPlayer Draw A Card
	| hypotheticalPlayer Make Terrain In hypotheticalTile
	| hypotheticalPlayer (Or hypotheticalPlayer)* Enters (hypotheticalCard | hypotheticalTile)
	| hypotheticalPlayer Play hypotheticalCard
	| hypotheticalPlayer Play playerRef First hypotheticalCard
	| hypotheticalPlayer Play playerRef Second hypotheticalCard
	| A Resource Is Removed In This Way
	| hypotheticalPlayer Attach hypotheticalCard To hypotheticalPlayer
	| hypotheticalCard Is Revealed
	| hypotheticalPlayer Stun hypotheticalPlayer
	| hypotheticalCard Is Sent To Your Discard
	| hypotheticalPlayer Second Wind
	| hypotheticalPlayer Play CARD_NAME
    | hypotheticalCard hypotheticalPlayer Played Fails To Resolve
    | hypotheticalPlayer Slow hypotheticalPlayer
    | hypotheticalPlayer Receive Control
	;

triggerEffect
    : At turnTrigger C effect
	| After afterEffectTrigger effectPostfix? C effect
    | durationEffect D effect
    | gameStartTrigger C effect
	;

//////////////////////////////////////////////////////////////////////////// replacement Trigger

ifWouldRecieveControl
    : If hypotheticalPlayer Would Receive Control C Instead player Receive The Control
    ;

ifWouldTakeDamage
    : If hypotheticalPlayer Would Take Damage For The First Time During Each Turn C playerRef? Instead Take literalValue Less Damage P This May Not Reduce Damage Taken Below literalValue
    ;

ifWouldHeal
    : If hypotheticalPlayer Would Heal hypotheticalPlayer HP C Instead Deal That Much Damage To player
    ;


replacementTriggerEffect
	: ifWouldRecieveControl
    | ifWouldTakeDamage
    | ifWouldHeal
	;

////////////////////////////////////////////////////////////////////////////


ifPlayerMakesChoice
	: If They Do
	| If They Dont
    | If You Do
    | If You Dont
	;

ifPreviousActionInvalid
	: If They Cant
	| If You Cant
	;

ifResolvedCard
	: If hypotheticalPlayer (Have | Has)? Resolved Another Ability Card This Turn
	| If hypotheticalPlayer (Have | Has)? Resolved A Attack Card This Turn
	;

ifUsedSecondWind
	: If hypotheticalPlayer (Have | Has)? Already? Used playerRef Second Wind
	;

ifNear
	: If hypotheticalPlayer Are Near CARD_NAME
	| If hypotheticalPlayer Are Near Terrain
	;

ifControlled
	: If hypotheticalPlayer Are Controlled
	;

ifPlayed
	: If hypotheticalPlayer (Have | Has)? Played A Attack Card This Turn
	;

ifTileHadTerrain
	: If previousTile Already Had Terrain
	;

ifCardRevealed
	: If player (Have | Has) At Least literalValue Revealed Card In Your Hand
	;

ifPlayerHasMitigate
	: If player (Have | Has) Mitigate
	;

ifPlayerDidntLeaveTile
	: If player Didnt Leave A Tile This Turn
	;

ifCardCostReduced
    : If Its Cost Is Reduced
    ;

ifPlayerHasMoreHP
    : If hypotheticalPlayer (Have | Has) More HP Than hypotheticalPlayer
    ;

ifPlayerHasNoActions
    : If hypotheticalPlayer (Have | Has) No Resource
    ;

ifPlayerInTerrain
    : If hypotheticalPlayer Are In Terrain
    ;

counterCardCondition
	: If hypotheticalCard Is Countered
	;

stunPlayerCondition
	: If A Resource Is Removed This Way
	;
revealCardsCondition
    : If hypotheticalCard Is Revealed This Way
    ;

condition
	: ifPlayerMakesChoice
	| ifPreviousActionInvalid
	| ifResolvedCard
	| ifUsedSecondWind
	| ifNear
	| ifControlled
	| ifPlayed
	| ifTileHadTerrain
	| ifCardRevealed
	| ifPlayerHasMitigate
    | ifPlayerDidntLeaveTile
    | ifCardCostReduced
    | ifPlayerHasMoreHP
    | ifPlayerHasNoActions
    | ifPlayerInTerrain
    | revealCardsCondition
    | stunPlayerCondition
    | counterCardCondition
	;

//////////////////////////////////////////////////////////////////////////// OMNI PRESENT EFFECTS


cardCantBeTargeted
	: CARD_NAME Cant Be Targeted By Card
	;

whileEffect
	: While CARD_NAME Is Revealed In Your Hand C passiveEffect
	;

primaryCostReduction
	: CARD_NAME Cost literalValue Less POWER For Each Other Revealed Card In Your Hand
	;

onlyPlayIf
	: Only Play CARD_NAME condition
	;

oncePerGameLimit
	: CARD_NAME May Be Played Once Per Game
	;

triggerLimit
    : CARD_NAME May Only Trigger condition
    ;

triggerTurnLimit
    : CARD_NAME May Trigger literalValue Per Turn
    ;


omniPresentEffect
    : cardCantBeTargeted
    | whileEffect
    | primaryCostReduction
    | onlyPlayIf
    | oncePerGameLimit
    | triggerLimit
    | triggerTurnLimit
    ;

//////////////////////////////////////////////////////////////////////////// misc

one: A | Once | One;
two: Two | Twice;
three: Three;
number: Integer;
playerPowerValue: player POWER;
playerControlAmountValue: The Total Value Of Control Affecting player;
playerHandSize: The Number Of hypotheticalCard In Their Hand;
playerMitigate: player Mitigate;

literalValue
    : one 
    | two 
    | three 
    | number 
    ;
gameStateValue
    : playerPowerValue
    | playerControlAmountValue
    | playerHandSize
    | playerMitigate
    ;



//////////////////////////////////////////////////////////////////////////// effects

chooseSameMultipleTimes: You May Choose The Same Mode literalValue;

effectList
    : D effect 
    | effectList P D effect
    ;
chooseEffect
    : Choose literalValue For Each playerFilter In It O effectList P chooseSameMultipleTimes
    | Choose literalValue O effectList P chooseSameMultipleTimes
    | Choose literalValue O effectList
    | Choose literalValue For Each playerFilter In It O effectList
    ;
  
singleEffect
    : chooseEffect
    | disarmPlayer
    | silencePlayer
    | slowPlayer
    | controlPlayer
    | travelPlayer
    | travelDistance
    | destroyCard
    | dealDamage
    | counterCard
    | attachCard
    | gainPower
    | gainAction
    | gainInitiative
    | gainMitigate
    | loseMitigate
    | useSecondWind
    | returnCard
    | putBottomCardOfDeckIntoHand
    | triggerDurationEffects
    | playCard
    | discardCards
    | gainsHeroic
    | healPlayer
    | stunPlayer
    | revealCards
    | drawCards
    | removeControl
    | hardcastEffect
    | seekEffect
    | makeTerrain
    | removeTerrain
    | spendResources
    | scryEffect
    | overrideFrenzy
    | increaseCardDuration
    | resetUseOfCard
    | heroic
    | spendResources
    ;

repeatedEffect
    : singleEffect For Each Instance Of Control playerRef Receive
    | singleEffect
    ;

conditionalEffect
    : condition C effect
    | repeatedEffect
    ;

alternativeEffect
    : condition C Instead effect
    | conditionalEffect
    ;

effect
    : hypotheticalPlayer May Have hypotheticalPlayer alternativeEffect
    | hypotheticalPlayer May alternativeEffect To alternativeEffect
    | hypotheticalPlayer May alternativeEffect
    | alternativeEffect Unless hypotheticalPlayer alternativeEffect
    | alternativeEffect
    ;

effectEnd
    : P Then C
    | P
    | And
    | C And
    | C Then
    | C
    | Then
    ;

anyEffect: effect | passiveEffect | replacementTriggerEffect | triggerEffect;

breachEffect: Breach D (anyEffect effectEnd)+;
advantageEffect: Advantage D (anyEffect effectEnd)+;
frenzyEffect: Frenzy D (anyEffect effectEnd)+;

line
    : omniPresentEffect P
    | breachEffect 
    | advantageEffect 
    | frenzyEffect
    | anyEffect effectEnd
    ;

text: line+ EOF;
