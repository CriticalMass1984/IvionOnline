parser grammar IvionParser;
options { tokenVocab = IvionLexer; }

generalPostInTerrainFilter: (In | With | That Already Had) Terrain;
generalPostNearTerrainFilter: Near Terrain;
generalPostNearPlayerFilter: Near hypotheticalPlayer;
generalPostNearTileFilter: Near hypotheticalTile;
generalPostInTileFilter: In hypotheticalTile;
generalPostNearCardFilter: Near hypotheticalCard;
generalPostInCardFilter: In hypotheticalCard;
generalPostPlayerChoiceFilter: Of player Choice; // TODO
generalPostAtLeastAwayFilter: At Least literalValue Tile Away;
generalPostAtLeastAwayFromPlayerFilter: At Least literalValue Tile Away From hypotheticalPlayer;

//////////////////////////////////////////////////////////////////////////// PLAYER

playerType
    : Player
    | Enemy
    | Ally
    ;

// really want to avoid right recursion, it's very slow
playerPreFix
    : Another? Controlled? Uncontrolled? playerType
    ;

playerPostFix
    : playerPreFix
    | playerPostFix generalPostInTerrainFilter
    | playerPostFix generalPostNearTerrainFilter
    | playerPostFix generalPostNearPlayerFilter
    | playerPostFix generalPostNearTileFilter
    | playerPostFix generalPostInTileFilter
    | playerPostFix generalPostNearCardFilter
    | playerPostFix generalPostInCardFilter
    | playerPostFix generalPostPlayerChoiceFilter
    | playerPostFix generalPostAtLeastAwayFilter
    | playerPostFix generalPostAtLeastAwayFromPlayerFilter
    ;

playerFilter
    : playerPostFix
    ;

currentPlayer
    : Yourself
    | You
    | Your
    ;
previousPlayer
    : Them
    | They
    | That playerType
    | Their
    ;
previousCardsOwner
    : Its Owner
    ;
previousCardsController
    : previousCard Controller
    | Its Controller
    ;
playerRef
    : previousPlayer
    | currentPlayer
    | previousCardsOwner
    | previousCardsController
    ;
targetPlayer
    : Another? Target playerFilter
    | upToFlag? literalValue Target playerFilter
    ;
allOtherPlayers
    : (All | Each) Other playerFilter
    ;
allPlayers
    : (Each | All | Any) playerFilter
    | Yourself Or Another Player
    ;

////
player
    : targetPlayer
    | playerRef
    | allPlayers
    | allOtherPlayers
    ;

hypotheticalPlayer
    : A? playerFilter
    | player
    ;

//////////////////////////////////////////////////////////////////////////// CARD

cardPreEnemyFilter: Enemy;
cardPreAttachedFilter: Attached;
cardPreAbilityFilter: Non? Ability;
cardPreAttackFilter: Non? Attack;
cardPreRevealedFilter: Reveal;
cardPreAnotherFilter: Another;
cardPreInstanceFilter: (| The | playerRef) (First | Second | Third);

cardPrefix
    : cardPreEnemyFilter
    | cardPreAttachedFilter
    | cardPreAbilityFilter
    | cardPreAttackFilter
    | cardPreRevealedFilter
    | cardPreAnotherFilter
    | cardPreInstanceFilter
    ;

cardPostTargettingFilter: Targeting hypotheticalPlayer;
cardPostCostFilter: With A Resource Cost Of literalValue Or (Less | More);
cardPostAttachedToPlayerFilter: Attached To hypotheticalPlayer;
cardPostAttachedToTileFilter: Attached To hypotheticalTile;
cardPostControlledByPlayerFilter: Controlled By hypotheticalPlayer;
cardPostHardcastFilter: With Hardcast;
cardPostFrenzyFilter: With Frenzy;
cardPostInDiscardFilter: From Your Discard;
cardPostTurnRefFilter: This Turn | During playerRef Turn;

cardPostfix
    : cardPostTargettingFilter
    | cardPostCostFilter
    | cardPostAttachedToPlayerFilter
    | cardPostAttachedToTileFilter
    | cardPostControlledByPlayerFilter
    | cardPostHardcastFilter
    | cardPostFrenzyFilter
    | cardPostInDiscardFilter
    | cardPostTurnRefFilter
    | generalPostInTerrainFilter
    | generalPostNearTerrainFilter
    | generalPostNearPlayerFilter
    | generalPostNearTileFilter
    | generalPostInTileFilter
    | generalPostNearCardFilter
    | generalPostInCardFilter
    | generalPostPlayerChoiceFilter
    | generalPostAtLeastAwayFilter
    | generalPostAtLeastAwayFromPlayerFilter
    ;

cardFilter
    : cardPrefix* Card cardPostfix*
    ;
cardName
    : CARD_NAME
    ;
bottomCard
    : The Bottom Card Of hypotheticalPlayer Deck
    ;
previousCard
    : That Card
    | The Card
    | Its
    | It
    | It From Your Discard
    ;
targetCard
    : Target cardFilter
    ;
selectCard
    : A cardFilter
    ;
singleCard
    : cardName
    | targetCard
    | bottomCard
    | previousCard
    | selectCard
    ;
allCards
    : All cardFilter
    ;
multiCard
    : UpToFlag=upToFlag? literalValue targetCard
    | UpToFlag=upToFlag? literalValue cardFilter
    ;
////
card
    : singleCard
    | allCards
    | multiCard
    ;

hypotheticalCard
    : A? cardFilter
    | card
    ;

//////////////////////////////////////////////////////////////////////////// TILE

tilePreEmptyFilter
    : Non? Empty
    ;

tilePreFix
    : tilePreEmptyFilter
    ;
tilePostFix
    : generalPostInTerrainFilter
    | generalPostNearTerrainFilter
    | generalPostNearPlayerFilter
    | generalPostNearTileFilter
    | generalPostInTileFilter
    | generalPostNearCardFilter
    | generalPostInCardFilter
    | generalPostPlayerChoiceFilter
    | generalPostAtLeastAwayFilter
    | generalPostAtLeastAwayFromPlayerFilter
    ;

tileFilter
    : tilePreFix* Tile tilePostFix*
    ;

targetTile: Another? literalValue? Target tileFilter;

previousTile
    : That Tile
    | The Tile
    ;
theNearestTile
    : The Nearest tileFilter Next To (hypotheticalPlayer | hypotheticalCard)
    ;
playersTile
    : The Tile player Is In
    ;
cardsTile
    : This Tile
    ;
singleTile
    : targetTile
    | previousTile
    | theNearestTile
    | playersTile
    | cardsTile
    ;

multiTile
    : UpToFlag=upToFlag? literalValue targetTile
    ;

allTiles
    : (Each | All) tileFilter
    ;

////

tile
    : singleTile
    | allTiles
    | multiTile
    ;
hypotheticalTile
    : A? tileFilter
    | tile
    ;

entity
    : card
    | player
    | tile
    ;

hypotheticalEntity
    : hypotheticalCard
    | hypotheticalPlayer
    | hypotheticalTile
    ;


//////////////////////////////////////////////////////////////////////////// EFFECTS


disarmPlayer
	: Disarm literalValue player
	| Disarm literalValue
	;

silencePlayer
	: Silence literalValue player
	| Silence literalValue
	;

slowPlayer
	: Slow literalValue player
	| Slow literalValue
	;

controlPlayer
	: Apply literalValue More Instances Of A Control Type Of Your Choice To player
	| Apply A Instance Of A Control Type Of Your Choice To player
	| Increase The Value Of A Control Type Already Affecting player By literalValue
	;

awayFlag
    : Away From player
    ;
upToFlag
    : Up To
    ;
travelDistance
    : Travel TargetPlayer=player? UpToFlag=upToFlag? Another? literalValue Tile Awayflag=awayFlag?
    ;

travelPlayer
	: Travel TargetPlayer=player? To (TargetTile=tile | TargetCard=card)
	;

destroyCard
	: Destroy card
	;

dealDamage
	: Deal value Damage To player
	| Deal Damage To player Equal To value
	| Deal value Damage To player Plus Damage Equal To value
	| Deal value Damage To
	| Deal Damage To player
	| Deal value More Damage To player
	| Deal value Damage To player Plus value
	| Deal Damage To player Equal To value
	;

counterCard
	: Counter card
	;

attachCard
	: Attach To entity
	| Attach To entity Until turnTrigger
	| Attach CARD_NAME To entity
	;

gainPower
	: player? (
        Gain literalValue POWER
        | Gain literalValue More POWER
        | Gain literalValue Additional POWER
        | Gain literalValue POWER
    )
	;

gainAction
	: player? (
        Gain literalValue ACTION
        | Gain literalValue More ACTION
        | Gain literalValue Additional ACTION
        | Gain literalValue ACTION
    )
	;

gainInitiative
	: Gain INITIATIVE+
	| player Gain INITIATIVE+
	| Gain Another INITIATIVE+
	;

gainMitigate
	: player Gains Mitigate literalValue
	| Gain Mitigate literalValue
	;

loseMitigate
	: Lose Your Mitigate
	;

useSecondWind
	: Use Your Second Wind
	;

returnCard
	: Return card To playerRef Hand
	;

putBottomCardOfDeckIntoHand
	: Put hypotheticalCard Into Your Hand
	;

triggerDurationEffects
	: Trigger Your Duration Effects As Though Your Turn Had Just Ended
	;

forLessResources
    : For literalValue Less Resource
    ;
freeFlag
    : For Free
    ;
playCard
	: Play card
	| Play card freeFlag
	| Play card forLessResources
    ;
playUltimate
	: Play Ultimate cardPostfix* P This Doesnt Count Towards Your Ultimate Limit Per Turn
	| Play Ultimate cardPostfix* freeFlag P This Doesnt Count Towards Your Ultimate Limit Per Turn
	| Play Ultimate cardPostfix* forLessResources P This Doesnt Count Towards Your Ultimate Limit Per Turn
	;

discardCards
	: Discard literalValue card
	| Discard card
	| player Discard literalValue card
	| player Discard card
	;

gainsHeroic
	: That Card Gains Heroic
	;

healPlayer
	: Heal player literalValue HP
	| Heal player HP Equal To value
	;

stunPlayer
	: Stun player
	;

revealCards
	: player Reveal literalValue Card From Their Hand
	| player Reveal literalValue Card From Their Hand
	| player Reveal A Card From Their Hand
	| player Reveal A Card From Their Hand
	| player Reveal literalValue Card From Their Hand
	| player Reveal A Card From Their Hand
	| Reveal CARD_NAME
	| Reveal A Card From Your Hand
    | Reveal A Card From Their Hand
	;

drawCards
	: Draw A Card
	| Draw literalValue Card
	| Draw A Additional Card
	| player Draw literalValue Card
	| player Draw literalValue Card
	| player Draw A Card
	;

removeControl
	: Remove Up To literalValue Instances Of Control From player
	| Remove All Control Affecting player
    | Remove A Instance Of Control From player
	;

hardcastEffect
	: Hardcast
	;

seekEffect
	: Seek
	;

makeTerrain
	: Make tile Difficult Terrain
	;

removeTerrain
	: Remove Target Terrain Token
	;

spendResources
	: Spend literalValue Resource
	;

scryEffect
	: Look At The Top literalValue Card Of Your Deck P Put Any Number On The Bottom And The Rest On Top In Any Order
	;

overrideFrenzy
	: That Card Have Frenzy Even If No Player Have Used Their Second Wind
	;

increaseCardDuration
	: Increase The Duration Of A Card You Control By literalValue
	;

resetUseOfCard
	: Reset The Use Of CARD_NAME
	;

heroic
    : Heroic
    ;

//////////////////////////////////////////////////////////////////////////// PASSIVE EFFECTS


onlyPlayCertainCards
	: hypotheticalPlayer May Only Play Card Named CARD_NAME
	| Only Play CARD_NAME If condition
	;

costReduction
	: hypotheticalCard hypotheticalPlayer Play Cost literalValue Less Resource
    | The Second Attack Card hypotheticalPlayer Play During Your Turn Cost literalValue Less Resource
	;

rangeSet
	: The Range Of Your Attack Card Is literalValue
	;

cantPlayCards
	: hypotheticalPlayer Cant Play Card
	;

playerCantBeTargeted
	: hypotheticalPlayer Cant Be Targeted By Enemies
	| hypotheticalPlayer Cant Be Targeted By Card
	;

increasedHandSize
    : Your Maximum Hand Size Is Increased By literalValue
    ;

increasedMaxHP
    : Your Maximum HP Is Increased By literalValue
    ;

replacesSecondWind
    : CARD_NAME Replaces Your Second Wind
    ;

noInitiative
    : hypotheticalPlayer Dont Gain INITIATIVE At The Start Of Your Turn
    ;
    
drawRange
	: All Of hypotheticalPlayer Card C Except Travel Card That Target A Tile C Also Draw Range From hypotheticalCard
	| All Of hypotheticalPlayer Card C Except Travel Card That Target A Tile C Also Draw Range From hypotheticalTile
	;

gainRange
	: All Of hypotheticalPlayer Card C Except Travel Card That Target A Tile C Gain literalValue Range
    ;

limitTargets
    : Only Target Other hypotheticalPlayer If condition
    ;

passiveEffect
    : playerCantBeTargeted
    | cantPlayCards
    | rangeSet
    | costReduction
    | onlyPlayCertainCards
    | increasedHandSize
    | increasedMaxHP
    | replacesSecondWind
    | noInitiative
    | drawRange
    | gainRange
    | limitTargets
    ;

//////////////////////////////////////////////////////////////////////////// PASSIVE TRIGGERS


durationEffect
	: Duration 
	;

startOfPlayerTurn
	: The Start Of hypotheticalPlayer Turn
	| The Start Of hypotheticalPlayer Turn
    ;

endOfPlayerTurnTrigger
    : The End Of hypotheticalPlayer Turn
	;
    
startOfPlayerNextTurn
    : The Start Of hypotheticalPlayer Next Turn
    ;

endOfTurnTrigger
    : The End Of The Turn
	| The End Of Turn
    | End Of Turn
	;

turnTrigger
    : startOfPlayerTurn
    | endOfPlayerTurnTrigger
    | startOfPlayerNextTurn
    | endOfTurnTrigger
    ;

gameStartTrigger
	: At The Start Of The Game
	;

effectPostfix
    : For The First Time During playerRef Turn
    | For The First Time During Each Turn 
    | During Each Turn
    | During playerRef Turn
    ;

ifPlayerMakesChoice
	: playerRef Do
	| playerRef Dont
	;

ifPreviousActionInvalid
	: playerRef Cant
	;

ifResolvedCard
	: hypotheticalPlayer Have? Resolve hypotheticalCard
	;

ifPlayedCard
	: hypotheticalPlayer Have? Play hypotheticalCard
	;

ifUsedSecondWind
	: hypotheticalPlayer Have? Already? Used playerRef Second Wind
	| hypotheticalPlayer Second Wind
	;


ifNear
	: hypotheticalEntity Is Near Terrain
	| hypotheticalEntity Is Near hypotheticalTile
	| hypotheticalEntity Is Near hypotheticalCard
	| hypotheticalEntity Is Near hypotheticalPlayer
	;

ifControlled
	: hypotheticalPlayer Is Controlled
	;

ifTileHadTerrain
	: previousTile Already Had Terrain
	;

ifCardRevealed
	: hypotheticalPlayer Have At Least literalValue Reveal Card In playerRef Hand
	;

ifPlayerHasMitigate
	: hypotheticalPlayer (Have | Gains) Mitigate
	;

ifPlayerDidntLeaveTile
	: hypotheticalPlayer Didnt Leave A Tile This Turn
	;

ifCardCostReduced
    : hypotheticalCard Cost Is Reduced
    ;

ifPlayerHasMoreHP
    : hypotheticalPlayer Have More HP Than hypotheticalPlayer
    ;

ifPlayerHasNoActions
    : hypotheticalPlayer Have No Resource
    ;

ifInTerrain
    : hypotheticalEntity Is In Terrain
    ;

counterCardCondition
	: hypotheticalCard Is Countered
	;

stunPlayerCondition
	: A Resource Is Removed In? This Way
	;
    
revealCardsCondition
    : hypotheticalCard Is Reveal In? This Way
    ;

playerControlledCondition: hypotheticalPlayer Is Controlled;
playerUncontrolledCondition: hypotheticalPlayer Is Uncontrolled;
playerHasCardsInDiscardCondition: hypotheticalPlayer Have At Least literalValue Card In playerRef Discard;
playerHasTakenDamageCondition: hypotheticalPlayer Have Taken literalValue Or More Damage This Turn;
cardResolvedCondition: hypotheticalCard Resolve;
playerDrewCardCondition: hypotheticalPlayer Draw A Card;
playerMadeTerrainCondition: hypotheticalPlayer Make Terrain In hypotheticalTile;
playerEntersSpaceCondition: hypotheticalPlayer (Or hypotheticalPlayer)* Enters (hypotheticalCard | hypotheticalTile);
playerAttachesCardCondition: hypotheticalPlayer Attach hypotheticalCard To hypotheticalPlayer;
cardIsRevealedCondition: hypotheticalCard Is Reveal;
playerStunsPlayerCondition: hypotheticalPlayer Stun hypotheticalPlayer;
cardSentToDiscardCondition: hypotheticalCard Is Sent To Your Discard;
cardFailsToResolveCondition: hypotheticalCard hypotheticalPlayer Play Fails To Resolve;
playerSlowsPlayerCondition: hypotheticalPlayer Slow hypotheticalPlayer;
playerRecivesControlCondition: hypotheticalPlayer Receive Control;

condition
	: playerControlledCondition
    | playerUncontrolledCondition
    | playerHasCardsInDiscardCondition
    | playerHasTakenDamageCondition
    | cardResolvedCondition
    | playerDrewCardCondition
    | playerMadeTerrainCondition
    | playerEntersSpaceCondition
    | playerAttachesCardCondition
    | cardIsRevealedCondition
    | playerStunsPlayerCondition
    | cardSentToDiscardCondition
    | cardFailsToResolveCondition
    | playerSlowsPlayerCondition
    | playerRecivesControlCondition
    | stunPlayerCondition
    | ifPlayerMakesChoice
    | ifPreviousActionInvalid
    | ifResolvedCard
    | ifUsedSecondWind
    | ifNear
    | ifControlled
    | ifPlayedCard
    | ifTileHadTerrain
    | ifCardRevealed
    | ifPlayerHasMitigate
    | ifPlayerDidntLeaveTile
    | ifCardCostReduced
    | ifPlayerHasMoreHP
    | ifPlayerHasNoActions
    | ifInTerrain
    | counterCardCondition
    | stunPlayerCondition
    | revealCardsCondition
	;

triggerEffect
    : At turnTrigger C effect
	| After condition effectPostfix? C effect
    | durationEffect D effect
    | gameStartTrigger C effect
	;


////////////////////////////////////////////////////////////////////////////




//////////////////////////////////////////////////////////////////////////// replacement Trigger

ifWouldRecieveControl
    : If hypotheticalPlayer Would Receive Control C Instead player Receive The Control
    ;

ifWouldTakeDamage
    : If hypotheticalPlayer Would Take Damage For The First Time During Each Turn C playerRef? Instead Take literalValue Less Damage P This May Not Reduce Damage Taken Below literalValue
    ;

ifWouldHeal
    : If hypotheticalPlayer Would Heal hypotheticalPlayer HP C Instead Deal That Much Damage To player
    ;

replacementTriggerEffect
	: ifWouldRecieveControl
    | ifWouldTakeDamage
    | ifWouldHeal
	;

//////////////////////////////////////////////////////////////////////////// OMNI PRESENT EFFECTS


cardCantBeTargeted
	: CARD_NAME Cant Be Targeted By Card
	;

whileEffect
	: While CARD_NAME Is Reveal In Your Hand C passiveEffect
	;

primaryCostReduction
	: CARD_NAME Cost literalValue Less POWER For Each Other Reveal Card In Your Hand
	;

onlyPlayIf
	: Only Play CARD_NAME If condition
	;

oncePerGameLimit
	: CARD_NAME May Be Play Once Per Game
	;

triggerLimit
    : CARD_NAME May Only Trigger If condition
    ;

triggerTurnLimit
    : CARD_NAME May Trigger literalValue Per Turn
    ;


omniPresentEffect
    : cardCantBeTargeted
    | whileEffect
    | primaryCostReduction
    | onlyPlayIf
    | oncePerGameLimit
    | triggerLimit
    | triggerTurnLimit
    ;

//////////////////////////////////////////////////////////////////////////// misc

one: A | Once | One;
two: Two | Twice;
three: Three;
number: Integer;
playerPowerValue: player POWER;
playerControlAmountValue: The Total Value Of Control Affecting player;
playerHandSize: The Number Of hypotheticalCard In Their Hand;
playerMitigate: player Mitigate;
playerCardsInHand: literalValue More? (| Damage | Slow | Silence | Disarm) For Each hypotheticalCard In playerRef Hand;

literalValue
    : one 
    | two 
    | three 
    | number 
    ;
gameStateValue
    : playerPowerValue
    | playerControlAmountValue
    | playerHandSize
    | playerMitigate
    | playerCardsInHand
    ;
value: gameStateValue | literalValue;


//////////////////////////////////////////////////////////////////////////// effects

chooseSameMultipleTimes: You May Choose The Same Mode literalValue;

effectList
    : D effect 
    | effectList P D effect
    ;

chooseEffect
    : Choose literalValue For Each playerFilter In It O effectList P chooseSameMultipleTimes
    | Choose literalValue O effectList P chooseSameMultipleTimes
    | Choose literalValue O effectList
    | Choose literalValue For Each playerFilter In It O effectList
    ;
  
singleEffect
    : chooseEffect
    | disarmPlayer
    | silencePlayer
    | slowPlayer
    | controlPlayer
    | travelPlayer
    | travelDistance
    | destroyCard
    | dealDamage
    | counterCard
    | attachCard
    | gainPower
    | gainAction
    | gainInitiative
    | gainMitigate
    | loseMitigate
    | useSecondWind
    | returnCard
    | putBottomCardOfDeckIntoHand
    | triggerDurationEffects
    | playCard
    | playUltimate
    | discardCards
    | gainsHeroic
    | healPlayer
    | stunPlayer
    | revealCards
    | drawCards
    | removeControl
    | hardcastEffect
    | seekEffect
    | makeTerrain
    | removeTerrain
    | spendResources
    | scryEffect
    | overrideFrenzy
    | increaseCardDuration
    | resetUseOfCard
    | heroic
    | spendResources
    ;

repeatedEffect
    : singleEffect For Each Instance Of Control playerRef Receive
    | singleEffect
    ;

conditionalEffect
    : If condition C effect
    | repeatedEffect
    ;

alternativeEffect
    : If condition C Instead effect
    | conditionalEffect
    ;

effect
    : hypotheticalPlayer May Have hypotheticalPlayer alternativeEffect
    | hypotheticalPlayer May alternativeEffect To alternativeEffect
    | hypotheticalPlayer May alternativeEffect
    | alternativeEffect Unless hypotheticalPlayer alternativeEffect
    | alternativeEffect
    ;

effectEnd
    : P Then C
    | P
    | And
    | C And
    | C Then
    | C
    | Then
    ;

anyEffect: effect | passiveEffect | replacementTriggerEffect | triggerEffect;

breachEffect: Breach D (anyEffect effectEnd)+;
advantageEffect: Advantage D (anyEffect effectEnd)+;
frenzyEffect: Frenzy D (anyEffect effectEnd)+;

line
    : omniPresentEffect P
    | breachEffect 
    | advantageEffect 
    | frenzyEffect
    | anyEffect effectEnd
    ;

text: line+ EOF;
