grammar Ivion;

//////////////////////////////////////////////////////////////////////

targetPlayer
    : 'target' 'player'
    ;

selectPlayer
    : A 'player'
    ;

cardController
    : 'you'
    | 'yourself'
    ;

previousPlayer
    : 'them'
    | 'they'
    ;

player
    : cardController
    | selectPlayer
    | previousPlayer
    | targetPlayer
    ;

damagePlayer
    : 'deal' integer 'damage' To player
    | player Take integer 'damage'
    ;

control
    : Slow | Silence | Disarm
    ;

controlPlayer
    : control integer player
    | player Is control integer
    ;

movePlayer
    : Move player? To? tile
    | Move player? integer Tile
    ;

travelPlayer
    : Travel player? To? tile
    | Travel player? integer Tile
    ;

gainActions
    : player? 'gain' integer 'actions';

gainPower
    : player? 'gain' integer 'power';

drawCards
    : player? Draw integer Card;

effectPlayer
    : damagePlayer
    | controlPlayer
    | movePlayer
    | travelPlayer
    | gainActions
    | gainPower
    | drawCards
    ;

Disarm : 'disarm' | 'disarmed';
Silence : 'silence' | 'silenced';
Slow : 'slow' | 'slowed';
Discard: 'discard' | 'discards';
Draw: 'draw' | 'draws';

Move: 'move' | 'moves';
Travel: 'travel' | 'travels';

//////////////////////////////////////////////////////////////////////

targetTile
    : 'target' Tile;

selectTile
    : A Tile
    ;

tile
    : selectTile
    | targetTile
    ;

Tile: 'tile' | 'tiles';

//////////////////////////////////////////////////////////////////////

targetCard
    : 'target' Card;

selectCard
    : A Card;

card
    : targetCard
    | selectCard
    ;


//////////////////////////////////////////////////////////////////////

endTheTurn: 'end' 'the' 'turn';
startTheTurn: 'start' 'the' 'turn';

miscEffect
    : startTheTurn
    | endTheTurn;

//////////////////////////////////////////////////////////////////////

line
    : (
        effectPlayer
        | miscEffect
    ) '.' ('\n' | EOF)?
    ;

paragraph
    : line+ '\n'*
    ;

text
    : paragraph+ | EOF;

/* built in data types */

integer
    : integerLiteral
    | integerWord
    ;
    
integerLiteral: Integer;
integerWord: A | One | Two | Three | Four | Five | Six;


// lexer
One: 'one' | 'One';
Two: 'two' | 'Two';
Three: 'three' | 'Three';
Four: 'four' | 'Four';
Five: 'five' | 'Five';
Six: 'six' | 'Six';

Card : 'card' | 'cards';

// articles
A: 'a' | 'A';
Is: 'is' | 'are';
To: 'to' | 'To';
Take: 'take' | 'takes';

/* built in data types */

Integer
    :    [0-9]+
    ;

Newline
    :  '\n'
    ;

// Skips
Whitespace
    :   [ \t]+
        -> skip
    ;

BlockComment
    :   '/*' .*? '*/'
        -> skip
    ;

LineComment
    :   '//' ~[\r\n]*
        -> skip
    ;