grammar Ivion;


////////////////////////////////////////////////////////////////////// Players

Heal : 'heal' | 'healed';
Disarm : 'disarm' | 'disarmed';
Silence : 'silence' | 'silenced';
Slow : 'slow' | 'slowed';
Control: 'control';
Discard: 'discard' | 'discards';
Draw: 'draw' | 'draws';
Deal: 'deal';
Move: 'move' | 'moves';
Travel: 'travel' | 'travels' ;
Target : 'target' | 'targeting' ;
Gain: 'gain' | 'gains';
Action: 'action' | 'actions';
Power: 'power';
Initiative: 'initiative';
SecondWind: 'second wind';
May: 'may';
Play: 'play';
Reveal: 'reveal' | 'reveals' | 'revealed';
Damage: 'damage' ;
Remove: 'remove';
Stun: 'stun';
Pay: 'pay' | 'plays' | 'paid' | 'spend' | 'spends';
Mitigate: 'mitigate' | 'mitigates' | 'mitigated';
Seek: 'seek';

Player : 'player' '\'s'? | 'players';
Enemy : 'enemy' '\'s'? | 'enemies';
Another: 'another';

Range: 'range';
Away: 'away';
Controller: 'controller';
Controlled: 'controlled';
UnControlled: 'uncontrolled';
Its: 'its' | 'it\'s';
Use: 'use' | 'uses';
UpTo: 'up to';
Have: 'have' | 'has';
Health: 'health' | 'hp';
For: 'for';
Less: 'less';
Free: 'free';
All: 'all';
Instances: 'instances';
More: 'more';
Removed: 'removed';
Way: 'way';
First: 'first';
During: 'during';
With: 'with';
Affecting: 'affecting';
Effect: 'effect' | 'effects';
Though: 'though';
Just: 'just';
Ended: 'ended';
As: 'as';
Had: 'had';

////////////////////////////////////////////////////////////////////// Tiles

Tile: 'tile' | 'tiles';
Empty: 'empty';
Enchanted: 'enchanted';
Difficult: 'difficult';
Terrain: 'terrain';
Make: 'make';
Token: 'token';
Near: 'near';
Nearest: 'nearest';
Next: 'next';

////////////////////////////////////////////////////////////////////// Cards

Card : 'card' | 'cards' | 'card\'s';

Heroic: 'heroic';
Attach: 'attach';
Attached: 'attached';
Hardcast: 'hardcast';

Return: 'return';
That: 'that';
Put: 'put';
Meta: 'meta';
Counter: 'counter' | 'counters' | 'countered';
Destroy: 'destroy';
Resource: 'resource' | 'resources';
resource: Resource | Action | Power;
Unless: 'unless';
Top: 'top';
Bottom: 'bottom';
Deck: 'deck';
On: 'On' | 'on';
Hand: 'hand';
From: 'from';
Until: 'until';
Number: 'number';
In: 'in';
It: 'it';
Or: 'or';
CARD_NAME: 'CARD_NAME';

////////////////////////////////////////////////////////////////////// Triggers

Trigger: 'trigger';
Turn: 'turn';
Start: 'start';
End: 'end';
At: 'at';
After: 'after' | At;
before: 'before';
This: 'this';
Then: 'then' | 'Then';
Enters: 'enter' | 'enters';
Time: 'time';
Duration: 'duration';
Any: 'any';

////////////////////////////////////////////////////////////////////// Replacement


If: 'if';
Do: 'do' | 'does' | 'did';
Dont: 'don\'t' | 'doesn\'t';
Resolve: 'resolve' | 'resolved' | 'resolves';
Used: 'used';
Non: 'non-';
Ability: 'ability';
Attack: 'attack';
Already: 'already';
Instead: 'instead';
Cost: 'cost';


////////////////////////////////////////////////////////////////////// Passive Modifiers

Also: 'also';
Except: 'except';
Cant: 'can\'t';
Be: 'be';
By: 'by';
While: 'while';
Only: 'only';
Named: 'named';

////////////////////////////////////////////////////////////////////// Misc

Choose: 'choose';
And: 'and';
Each: 'each';
Owners: 'owner\'s';

// articles
A: 'a' | 'an';
Is: 'is' | 'are';
To: 'to';
Take: 'take' | 'takes';
Of: 'of';
The: 'the';

One: 'one';
Two: 'two';
Three: 'three';
Four: 'four';
Five: 'five';
Six: 'six';

Integer
    :    [0-9]+
    ;

Newline
    :  '\n'
    ;

// Skips
Whitespace
    :   [ \t]+
        -> skip
    ;

//////////////////////////////////////////////////////////////////////

playerPreFilter
    : Another
    | Each
    | Any
    | A
    ;

playerPostFilter
    : Near Terrain
    | In Terrain
    | Controlled
    | UnControlled
    | Near card
    ;

playerFilter
    : playerPreFilter* (Player | Enemy) playerPostFilter*
    ;

targetPlayer
    : (UpTo integer)? playerPreFilter* Target playerFilter
    ;

selectPlayer
    : UpTo? integer? playerFilter
    ;

previousCardsController
    : Its Controller
    | That Card Controller
    | The Card Controller
    ;

cardController 
    : 'you' | 'yourself' | 'your'
    ;

previousPlayer
    : 'them' | 'they' | 'their'
    | That (Player | Enemy)
    ;

player
    : cardController
    | selectPlayer
    | previousPlayer
    | targetPlayer
    | previousCardsController
    ;

///////////////

damagePlayer
    : Deal integer More? Damage To player
    | player Take integer Damage
    | Deal Damage To player 'equal' To integer
    ;

controlPlayer
    : (Control | Slow | Silence | Disarm) integer player
    | (Control | Slow | Silence | Disarm) integer
    | player Is (Control | Slow | Silence | Disarm) integer
    ;

movePlayer
    : Move To tile
    ;

travelPlayer
    : Travel To tile
    | Travel player To tile
    | Travel player To card
    | Travel player? UpTo? integer Tile
    | Travel player? UpTo? integer Tile Away From player
    ;

gainResource
    : player? Gain integer? (More | Another)? (resource | Initiative)
    ;

payResource
    : player? May? Pay integer resource
    ;

drawCards
    : player Draw integer Card
    | Draw integer Card
    ;

discardCards
    : player Discard integer Card
    | Discard integer Card
    ;

useSecondWind
    : Use (cardController | previousPlayer) SecondWind;

playACard
    : player May? Play card
    | player May? Play card For integer Less resource
    | player May? Play card For Free
    ;

heal
    : Heal player integer Health;

revealFromHand
    : player Reveal integer Card From previousPlayer Hand
    ;

removeControl
    : Remove UpTo integer Instances Of Control From player
    | Remove All Control Affecting player
    ;

stunPlayer
    : Stun player
    ;

gainsMitigate
    : player? Gain Mitigate integer
    ;

seek: Seek;

playerEffect
    : damagePlayer
    | controlPlayer
    | movePlayer
    | travelPlayer
    | gainResource
    | drawCards
    | useSecondWind
    | playACard
    | heal
    | revealFromHand
    | discardCards
    | removeControl
    | stunPlayer
    | payResource
    | gainsMitigate
    | seek
    ;

//////////////////////////////////////////////////////////////////////

tilePreFilter
    : Each
    | Another
    | Empty
    | Nearest
    ;

tilePostFilter
    : Near player
    | Near Terrain
    | Near card
    | player Is? In
    | Next To player
    | Next To Terrain
    ;

targetTile
    : (UpTo integer)? tilePreFilter* Target tilePreFilter* Tile tilePostFilter*
    ;

selectTile
    : (The | A|) tilePreFilter* Tile tilePostFilter*
    ;

previousTile
    : That Tile
    | It
    | This Tile
    ;

tile
    : selectTile
    | targetTile
    | previousTile
    ;


//////////////////////////////////////////////////////////////////////


targetTerrain
    : Target Terrain Token
    ;

terrain
    : targetTerrain
    ;

///////////////

makeTerrain
    : player? Make tile (Difficult | Enchanted) Terrain
    | player? Make (Difficult | Enchanted | ) Terrain In tile
    ;
destroyTerrain: Remove terrain;

terrainEffect
    : makeTerrain
    | destroyTerrain
    ;


//////////////////////////////////////////////////////////////////////

cardFilterPrefix
    : Another
    | Non? (Attack | Ability)
    | player First
    | All
    | Enemy
    | Each
    | Attached
    ;

cardFilterPostfix
    : Target player
    | During player Turn
    | From player Discard
    | player Play
    | With A resource Cost Of integer Or Less
    | Near thisCard
    | Near player
    | Near tile
    | Controlled By player
    ;

targetCard
    : (UpTo integer)? cardFilterPrefix* Target cardFilterPrefix* Card cardFilterPostfix*
    ;

selectCard
    :  integer cardFilterPrefix* Card cardFilterPostfix*
    |  A? cardFilterPrefix* Card cardFilterPostfix*
    ;

previousCard
    : That Card
    | The Card
    ;

thisCard
    : CARD_NAME
    ;

topOfStack
    : Top Of The 'stack'
    ;

bottomOfDeck
    : The Card On The Bottom Of player Deck
    | The Bottom Card Of player Deck
    ;

topOfDeck
    : The Card On The Top Of player Deck
    | The Top Card Of player Deck
    ;

hand
    : player Hand;

card
    : previousCard
    | targetCard
    | selectCard
    | topOfStack
    | bottomOfDeck
    | topOfDeck
    | From hand
    | thisCard
    ;


///////////////

attachCard
    : Attach To player
    | Attach To player Until The? End Of The? Turn
    | Attach To tile
    | Attach To tile Until The? End Of The? Turn
    ;

metaCounterCard
    : Meta Counter card
    ;

counterCard
    : Counter card
    ;

destroyCard
    : Destroy card
    ;

cardGainsTrait
    : card Gain (Heroic)
    ;

putCard
    : Put card (On bottomOfDeck | topOfDeck | 'into' hand);

hardcast
    : Hardcast;

resolveCard
    : player Resolve card
    | card Resolve
    ;

returnCardToHand
    : (player May)? Return card To Its Owners Hand
    | (player May)? Return card From player Discard To (cardController | previousPlayer) Hand
    ;

triggerDuration
    : Trigger player Duration Effect As Though (cardController | previousPlayer) Turn Had Just Ended
    ;

cardEffect
    : metaCounterCard
    | counterCard
    | destroyCard
    | attachCard
    | putCard
    | cardGainsTrait
    | hardcast
    | resolveCard
    | returnCardToHand
    | triggerDuration
    ;


////////////////////////////////////////////////////////////////////// triggers

turnTrigger
    : At The (Start | End) Of player Turn
    ;

effectTrigger
    : After effect
    ;

tileTrap
    : After player Enters tile
    | After player
    ;

duration
    : Duration '-'
    ;

forTheFirstTime: For The First Time During Each Turn;

triggerEffect
    : effectTrigger forTheFirstTime? ','? effect 
    | turnTrigger ',' effect
    | tileTrap ',' effect
    | duration effect
    ;

////////////////////////////////////////////////////////////////////// conditions

resolved: If player Have Resolve (A | Another) Non? (Ability | Attack) Card This Turn;
usedSecondWind: If player Have Already Used (cardController | previousPlayer) SecondWind;
stunSuccess: If A resource Is Removed This Way;
counterSuccess: If card Is Counter;
ifPlayerMakesChoice : If (cardController | previousPlayer) (Do | Dont);
ifPlayerIsNear: If player Is Near (tile | card);

condition
    : resolved
    | usedSecondWind
    | ifPlayerMakesChoice
    | stunSuccess
    | counterSuccess
    | ifPlayerIsNear
    ;

breach: 'breach' '-';
advantage: 'advantage' '-';
frenzy: 'frenzy' '-';

conditionalEffect
    : condition ',' Instead? effect
    | breach effect
    | advantage effect
    | frenzy effect
    ;

////////////////////////////////////////////////////////////////////// Passive Modifiers

cardModifier
    : card Cost integer Less resource
    ;

rangeSource
    : All Of cardController Card ',' Except Travel Card That Target A Tile ',' Also Draw Range From (Terrain | card)
    ;

onlyPlayName
    : player May Only Play Card Named CARD_NAME
    ;

rangeSet
    : The Range Of player card Is integer
    ;

cantTarget
    : player Cant Be Target By playerFilter
    ;

passiveModifier
    : cardModifier
    | rangeSource
    | onlyPlayName
    | rangeSet
    | cantTarget
    ;

////////////////////////////////////////////////////////////////////// Passive Modifiers At All Times


cantTouchThis: card Cant Be Target By Card;
whileRevealed: While card Is Reveal In player Hand ',' passiveModifier;
cantPlayCardUnless: Only Play CARD_NAME condition; 

omnipotentEffect
    : cantTouchThis
    | whileRevealed
    | cantPlayCardUnless
    ;


//////////////////////////////////////////////////////////////////////

simpleEffect
    : (Then ',' | Then |) (condition ',')? 
    (
        playerEffect
        | cardEffect
        | terrainEffect
        | passiveModifier
        | omnipotentEffect
    ) 
    (Unless playerEffect)?
    (',' | '.' | And | ',' And)
    ;

effect
    : simpleEffect
    | Choose integer ':' ('-' simpleEffect)+
    | Choose integer For Each playerFilter In tile ':' ('-' simpleEffect)+
    ;

//////////////////////////////////////////////////////////////////////


text
    : (
        conditionalEffect
        | triggerEffect 
        | effect)+ 
        EOF;

//////////////////////////////////////////////////////////////////////


theNumberOfCardsInHand
    : The Number Of Card In player Hand
    | The Number Of Reveal Card In player Hand
    ;
integerLiteral: Integer;
integerWord: A | One | Two | Three | Four | Five | Six;

integer
    : integerLiteral
    | integerWord
    | theNumberOfCardsInHand
    ;

